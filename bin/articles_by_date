#!/usr/bin/perl
use v5.32;
use experimental qw(signatures);

use FindBin;
use lib $FindBin::Bin;
use open qw(:std :utf8);

use Data::Dumper;
use POSIX;
use Term::ANSIColor;

require 'articles_utils.pl';

say
	join "\n",
	map  {
		join '  ',
			$_->[1]{date},
			$_->[0],
			colored( [ $_->[2] ], $_->[1]{title} )
		}
	sort { $a->[1]{date} cmp $b->[1]{date} }
	map  {
		my $meta  = get_article_meta($_);
		my $color = get_article_color( $meta );
		[ $_, $meta, $color ]
		}
	get_article_files()->@*
	;


sub get_article_color ( $meta ) {
	state $now = strftime( "%Y-%m-%dT%H:%M:%S", localtime );
	my $date = $meta->{date};

	return 'magenta' if $meta->{draft};
	return 'green'  if $now gt $meta->{date};
	return 'blue';
	}
