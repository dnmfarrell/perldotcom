#!perl

=head1 NAME

tag_the_build - make a JSON file that captures the current repo state

=head1 SYNOPSIS

	% perl bin/tag_the_build > build.json
	{"branch":"master","commit":"...",
	"datetime":"Wed Nov 18 13:00:00 2020","time":1605722400}

From the F<Makefile>, run the C<json> target.

	% make json

Once this file is live on the site, you can check the status of the
live site:

	% make show_live_build

=head1 DESCRIPTION

The Perl.com development website happens in a GitHub repository, but
builds the site into a different repository. Pushing to that repo
is eventually picked up by the bits that make the site live. That could
take awhile to update.

With that lag, we are sometimes curious which version is live. This
program produces a little bit of JSON with the commit, branch, and
time for the deployment.

=cut

use Mojo::JSON qw(encode_json);

chomp( my $commit = `git rev-parse HEAD` );
chomp( my $branch = `git rev-parse --abbrev-ref HEAD` );

my $data = {
	time     => time,
	datetime => scalar localtime,
	commit   => $commit,
	branch   => $branch,
	};

print encode_json($data);
